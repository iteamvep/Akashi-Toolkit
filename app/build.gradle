apply plugin: 'com.android.application'

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.3"

    defaultConfig {
        applicationId "rikka.akashitoolkit"
        minSdkVersion 15
        targetSdkVersion 23
        versionCode 76
        versionName "0.0.35"

        vectorDrawables.useSupportLibrary = true

        buildConfigField "long", "TIMESTAMP", System.currentTimeMillis() + "L"
    }

    packagingOptions{
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
    }

    lintOptions {
        abortOnError false
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }

        debug {
            applicationIdSuffix '.debug'
        }
    }

    productFlavors {
        china {
            buildConfigField "boolean", "isGooglePlay", "false"
        }

        play {
            buildConfigField "boolean", "isGooglePlay", "true"
        }
    }

    sourceSets.china {
        manifest.srcFile 'src/china/AndroidManifest.xml'
    }

    sourceSets.play {
        manifest.srcFile 'src/play/AndroidManifest.xml'
    }

    applicationVariants.all { variant ->
        variant.outputs.each { output ->
            if (output.outputFile != null && output.outputFile.name.endsWith('.apk')) {
                output.outputFile = file("${output.outputFile.parent}/akashitoolkit" +
                        "-${variant.versionCode}" +
                        "-${variant.flavorName.toLowerCase()}" +
                        ".apk")
            }
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    testCompile 'junit:junit:4.12'
    compile 'com.android.support:appcompat-v7:23.4.0'
    compile 'com.android.support:design:23.4.0'
    compile 'com.android.support:cardview-v7:23.4.0'
    compile 'com.android.support:gridlayout-v7:23.4.0'
    compile 'com.squareup.retrofit2:retrofit:2.0.0-beta4'
    compile 'com.squareup.retrofit2:converter-gson:2.0.0-beta4'
    compile 'com.google.code.gson:gson:2.6.2'
    compile 'com.github.bumptech.glide:glide:3.7.0'
    compile 'com.squareup:otto:1.3.8'
    //compile 'com.github.RikkaW:DayNight-Theme:v1.0.13'
    compile (name:'daynightmode-release', ext:'aar')
    compile 'com.github.RikkaW:MaterialPreference:v0.0.7'
    compile 'com.github.RikkaW:MiniDrawer:v0.0.5'

    chinaCompile 'cn.leancloud.android:avoscloud-sdk:v3.13.8'
    chinaCompile 'cn.leancloud.android:avoscloud-statistics:v3.13.8'
    chinaCompile 'cn.leancloud.android:avoscloud-push:v3.13.8@aar'
    chinaCompile 'cn.leancloud.android:Java-WebSocket:1.3.1-leancloud'
    chinaCompile 'com.google.protobuf:protobuf-java:2.6.1'

    playCompile "com.google.android.gms:play-services-gcm:8.4.0"

    compile 'com.github.chrisbanes:PhotoView:1.2.6'

    compile 'io.reactivex:rxjava:1.1.2'
    compile 'io.reactivex:rxandroid:1.1.0'
}

apply plugin: 'com.google.gms.google-services'