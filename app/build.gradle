apply plugin: 'com.android.application'
apply plugin: 'com.getkeepsafe.dexcount'

def gitCommitCount = Integer.parseInt('git rev-list --count HEAD'.execute([], project.rootDir).text.trim())

android {
    compileSdkVersion 24
    buildToolsVersion "24.0.2"

    defaultConfig {
        applicationId "moe.kcwiki.akashitoolkit"
        minSdkVersion 16
        targetSdkVersion 24
        versionCode gitCommitCount
        versionName "0.12.0"

        vectorDrawables.useSupportLibrary = true

        buildConfigField "String", "BASE64_PUBLIC_KEY", "\"${base64_public_key}\""
        buildConfigField "String", "LEANCLOUD_APP_ID", "\"${leancloud_app_id}\""
        buildConfigField "String", "LEANCLOUD_APP_KEY", "\"${leancloud_app_key}\""

        buildConfigField "long", "TIMESTAMP", System.currentTimeMillis() + "L"
    }

    packagingOptions {
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
    }

    lintOptions {
        abortOnError false
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }

        googlePlayRelease {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

            applicationIdSuffix '.lite'
        }

        debug {
            applicationIdSuffix '.debug'
        }
    }

    productFlavors {
        leancloud {
        }

        firebase {
        }
    }

    sourceSets.leancloud {
        manifest.srcFile 'src/leancloud/AndroidManifest.xml'
    }

    sourceSets.firebase {
        manifest.srcFile 'src/firebase/AndroidManifest.xml'
    }

    applicationVariants.all { variant ->
        variant.outputs.each { output ->
            if (output.outputFile != null && output.outputFile.name.endsWith('.apk')) {
                String f = "${output.outputFile.parent}/akashitoolkit-${variant.versionCode}";

                if (variant.buildType.name.startsWith("googlePlay")) {
                    f += "-googleplay";
                } else if (variant.flavorName.equals("firebase")) {
                    f += "-firebase";
                }

                f += ".apk";

                output.outputFile = file(f)
            }
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    testCompile 'junit:junit:4.12'
    compile 'com.android.support:appcompat-v7:24.2.1'
    compile 'com.android.support:design:24.2.1'
    compile 'com.android.support:cardview-v7:24.2.1'
    compile 'com.android.support:gridlayout-v7:24.2.1'

    compile 'com.squareup.retrofit2:retrofit:2.1.0'
    compile 'com.squareup.retrofit2:converter-gson:2.1.0'

    compile 'com.google.code.gson:gson:2.7'

    compile 'com.github.bumptech.glide:glide:3.7.0'

    compile 'com.squareup:otto:1.3.8'
    //compile 'com.github.RikkaW:DayNight-Theme:v1.0.13'
    compile (name:'daynightmode-release', ext:'aar')

    compile 'com.github.RikkaW:MaterialPreference:v1.3'
    compile 'com.github.RikkaW:MiniDrawer:v0.0.6'

    leancloudCompile 'cn.leancloud.android:avoscloud-sdk:v3.14.5'
    leancloudCompile 'cn.leancloud.android:avoscloud-push:v3.14.5@aar'
    leancloudCompile 'cn.leancloud.android:avoscloud-statistics:v3.14.5@aar'
    leancloudCompile 'cn.leancloud.android:Java-WebSocket:1.3.2-leancloud'
    leancloudCompile 'com.google.protobuf:protobuf-java:2.6.1'

    firebaseCompile 'com.google.android.gms:play-services-base:9.6.1'
    firebaseCompile 'com.google.firebase:firebase-core:9.6.1'
    firebaseCompile "com.google.firebase:firebase-messaging:9.6.1"
    firebaseCompile "com.google.firebase:firebase-crash:9.6.1"

    compile 'com.github.chrisbanes:PhotoView:1.2.6'

    //compile 'com.google.android.gms:play-services-analytics:9.6.1'

    compile 'io.reactivex:rxjava:1.1.2'
    compile 'io.reactivex:rxandroid:1.1.0'

    compile 'com.zzhoujay.markdown:markdown:0.0.10'

    compile 'com.github.rubensousa:gravitysnaphelper:0.1'
}